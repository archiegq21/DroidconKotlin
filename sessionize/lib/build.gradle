apply plugin: 'kotlin-multiplatform'
apply plugin: 'org.jetbrains.kotlin.native.cocoapods'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.android.library'
apply plugin: 'com.squareup.sqldelight'
apply plugin: 'co.touchlab.kotlinxcodesync'

android {
    compileSdkVersion 30
    testOptions.unitTests.includeAndroidResources = true

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        //This is for MultiplatformSettings
        debug {
            // MPP libraries don't currently get this resolution automatically
            matchingFallbacks = ['release']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "11"
        }
    }

    /** Work Around for https://stackoverflow.com/questions/65372825/kotlin-multiplatform-configuration-issue
     * TODO Remove later
     */
    configurations {
        create("androidTestApi")
        create("androidTestDebugApi")
        create("androidTestReleaseApi")
        create("testApi")
        create("testDebugApi")
        create("testReleaseApi")
    }
}

dependencies {
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation "androidx.core:core:1.5.0"
}

version = "1.0"

kotlin {
    targets {
        android()
//        iosArm64()
        def onPhone = System.getenv('SDK_NAME')?.startsWith("iphoneos")
        if(onPhone){
            iosArm64("ios")
        }else{
            iosX64("ios")
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation "co.touchlab:stately-common:${versions.stately}"
                implementation "co.touchlab:stately-concurrency:${versions.stately}"
                implementation "co.touchlab:stately-collections:${versions.stately}"

                implementation deps.sqldelight.runtime
                implementation deps.multiplatformSettings
                implementation deps.serialization.commonRuntime
                implementation deps.firebase.firestore
                implementation deps.kotlin.coroutines.common
            }
        }

        commonTest {
            dependencies {
                implementation deps.kotlin.test.common
                implementation deps.kotlin.test.annotations
                implementation "co.touchlab:stately-common:${versions.stately}"
                implementation "co.touchlab:stately-concurrency:${versions.stately}"
                implementation "co.touchlab:stately-collections:${versions.stately}"
                implementation deps.multiplatformSettings
                implementation deps.testhelp
                implementation deps.kotlin.coroutines.common
            }
        }

        iosMain {
            dependencies {
                implementation deps.sqldelight.driverIos
                implementation deps.sqliter.ios
            }
        }

        androidMain {
            dependencies {
                implementation deps.sqldelight.driverAndroid
                implementation deps.firebase.firestoreAndroid
            }
        }

        androidTest {
            dependencies {
                implementation deps.kotlin.test.jvm
                implementation deps.kotlin.test.junit
                implementation deps.sqldelight.driverAndroid
                implementation deps.kotlin.coroutines.android
                implementation 'junit:junit:4.13.2'
                implementation "androidx.test:core:1.3.0"
                implementation "androidx.test.ext:junit:1.1.2"
                implementation "org.robolectric:robolectric:4.0"
            }
        }
    }

    cocoapods {
        // Configure fields required by CocoaPods.
        summary = "Lots of Droidcon Stuff"
        homepage = "https://github.com/touchlab/DroidconKotlin"
    }
}

sqldelight {
    DroidconDb {
        packageName = "co.touchlab.droidcon.db"
    }
}

xcode {
    projectPath = "../../iosApp/iosApp.xcodeproj"
    target = "iosApp"
}

