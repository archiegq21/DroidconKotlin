apply plugin: 'com.android.application'
apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlin-android-extensions'

def firebaseEnabled = file("./google-services.json").exists()
def releaseEnabled = file("./release.jks").exists()

if (firebaseEnabled) {
    apply plugin: 'io.fabric'
}

Properties properties = new Properties()
try {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}
catch(Exception e) {

}

def releasePassword = properties.getProperty('releasePassword', '')

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "co.touchlab.droidconsf2018"
        buildConfigField "String", "TIME_ZONE", "\"America/Los_Angeles\""
        buildConfigField "boolean", "FIREBASE_ENABLED", "${firebaseEnabled}"

        minSdkVersion 24
        targetSdkVersion 30
        versionCode 20000
        versionName "2.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }
    if(releaseEnabled) {
        signingConfigs {
            release {
                storeFile file("release.jks")
                keyAlias "key0"
                storePassword "$releasePassword"
                keyPassword "$releasePassword"
            }
        }
    }

    buildTypes {
        if(releaseEnabled) {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.release
            }
        }

        //This is for MultiplatformSettings
        debug {
            // MPP libraries don't currently get this resolution automatically
            matchingFallbacks = ['release']
        }
    }

    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "11"
        }
    }

    /** Work Around for https://stackoverflow.com/questions/65372825/kotlin-multiplatform-configuration-issue
     * TODO Remove later
     */
    configurations {
        create("androidTestApi")
        create("androidTestDebugApi")
        create("androidTestReleaseApi")
        create("testApi")
        create("testDebugApi")
        create("testReleaseApi")
    }
}

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation project(':sessionize:lib')
            }
        }
    }

    targets {
        fromPreset(presets.android, 'androidApp')
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.nex3z:flow-layout:1.2.2'
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'

    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation 'com.squareup.picasso:picasso:2.71828'

    implementation deps.sqldelight.runtimeJdk
    implementation deps.sqldelight.driverAndroid
    implementation deps.kotlin.coroutines.android
    implementation deps.firebase.androidCore
    implementation deps.firebase.androidMessaging
    implementation deps.crashlytics

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation deps.multiplatformSettings

    implementation "androidx.navigation:navigation-fragment-ktx:2.4.0-alpha01"
    implementation "androidx.navigation:navigation-ui-ktx:2.4.0-alpha01"
    implementation deps.firebase.firestoreAndroid
}

apply plugin: "androidx.navigation.safeargs"


if (firebaseEnabled) {
    apply plugin: 'com.google.gms.google-services'
}
